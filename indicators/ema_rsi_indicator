// This indicator uses different length EMAs and RSI to find potential uptrends
// Plus there is a Buy marker in it, which marks the best entry points

//@version=6
indicator(title="EMA+RSI Indicator", shorttitle="EmaRsiInd", overlay=true)

// EMA calculations
ema42 = ta.ema(close, 42)
ema100 = ta.ema(close, 100)
ema200 = ta.ema(close, 200)
plot(ema42, title="EMA42", color=color.red)
plot(ema100, title="EMA100", color=color.green)
plot(ema200, title="EMA200", color=color.blue)
// Price and EMAs must be aligned in incremental order
ema_cond = low > ema42 and ema42 > ema100 and ema100 > ema200
// EMA derivatives must be positive
ema_diff_cond = ema42 > ema42[1] and ema100 > ema100[1] and ema200 > ema200[1]

// RSI
len_rsi = 14
up = ta.rma(math.max(ta.change(close), 0), len_rsi)
down = ta.rma(-math.min(ta.change(close), 0), len_rsi)
rsi = down == 0 ? 100 : up == 0 ? 0 : 100 - (100 / (1 + up / down))
// RSI must be high
rsi_cond = rsi >= 50

// Color background to green if all conditions are met
bg_col = ema_cond and ema_diff_cond and rsi_cond ? #00FF0030 : #FF000030
bgcolor(bg_col)

// Buy marker - put green triangles where the following conditions are met for a given time:
// - there were no large losses (no too small low values compared to close)
// - there were large wins (large high values compared to close)
// Warning - this is not an indicator b/c it looks into the future
bool no_loss = true
bool win = false
bool buy_marker = false
n = 10
for i = 1 to n
    no_loss := no_loss and close[n] * 0.99 <= low[n-i]
    win := win or close[n] * 1.02 <= high[n-i] 
buy_marker := win and no_loss
plotshape(buy_marker, offset=-n, style=shape.triangleup, location=location.belowbar, color=color.green, size=size.small)  
